set encoding=utf8
using modex DateTime-modex.xml,Numex-modex.xml
using groups DateTime,Numex
set defaultAction=>CreateSpecificEntity()


@day=(lundi,mardi,mercredi,jeudi,vendredi,samedi,dimanche)

@month=(janvier,février,mars,avril,mai,juin,juillet,août,septembre,octobre,novembre,décembre)

@numday=(
t_integer>1<31,
premier,
1er,
deux,
trois,
quatre,
cinq,
six,
sept,
huit,
neuf,
dix,
onze,
douze,
treize,
quatorze,
quinze,
seize,
dix-sept,
dix-huit,
dix-neuf,
vingt,
vingts,
vingt-deux,
vingt-trois,
vingt-quatre,
vingt-cinq,
vingt-six,
vingt-sept,
vingt-huit,
vingt-neuf,
trente,
trente\ et\ un
)

@nummonth=(t_integer>1<12)

@numyear=(t_integer>1700<2050,t_integer>1<99)
@numyear4=(t_integer>1700<2050)

# lundi 22 mai 1968
@day::@numday @month @numyear?:DATE:
+AddEntityFeature(trigger,"day")
+AddEntityFeature(right.1,"numday")
+AddEntityFeature(right.2,"month")
+AddEntityFeature(right.3,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

# jours simples
@daytime=(matin,midi,après-midi,soir)

@day::[t_integer] [(\:|H|h)] [t_integer]:DATE:
+AddEntityFeature(trigger,"day")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

@day:ce::DATE:
+AddEntityFeature(trigger,"day")
+AddEntityFeature(left.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

@day::prochain:DATE:
+AddEntityFeature(trigger,"day")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate("next_day")

@day::(dernier|@daytime|@numday)?:DATE:
+AddEntityFeature(trigger,"day")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

#------------------------------
@modifierDate=(début,mi,milieu,fin);

#------------------------------
# annonceur mois pour période

@month:(de|d\') (@modifierDate)?:(@numyear)? à (@modifierDate)? @month (@numyear)?:DATE:
+AddEntityFeature(trigger,"month")
+AddEntityFeature(right.1,"numyear")
+AddEntityFeature(right.4,"monthend")
+AddEntityFeature(right.5,"numyearend")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate("isInterval")

<NUMBER>:[du]:(@month)? (@numyear)? [au] [(@numday|<NUMBER>)] [@month] [(@numyear)?]:DATE:
+AddEntityFeature(trigger,"numday")
+AddEntityFeature(right.1,"month")
+AddEntityFeature(right.2,"numyear")
+AddEntityFeature(right.4,"numdayend")
+AddEntityFeature(right.5,"monthend")
+AddEntityFeature(right.6,"numyearend")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

@month:[du] [(@numday|<NUMBER>)] [(@month)?] [(@numyear)?] [au] (@numday|<NUMBER>):(@numyear)?:DATE:
+AddEntityFeature(left.2,"numday")
+AddEntityFeature(left.3,"month")
+AddEntityFeature(left.4,"numyear")
+AddEntityFeature(left.6,"numdayend")
+AddEntityFeature(trigger,"monthend")
+AddEntityFeature(right.1,"numyearend")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

#------------------------------

# fin juin
@month:@modifierDate::DATE:
+AddEntityFeature(trigger,"month")
+AddEntityFeature(left.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

# 22 mai 1968
# 22 mai
# mai 1968
# mai
# 18 juin 40
# 31 octobre prochain

@month:@numday?:@numyear:DATE:
+AddEntityFeature(left.1,"numday")
+AddEntityFeature(trigger,"month")
+AddEntityFeature(right.1,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

@month:@numday?:(dernier|suivant)?:DATE:
+AddEntityFeature(left.1,"numday")
+AddEntityFeature(trigger,"month")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

@month::prochain:DATE:
+AddEntityFeature(trigger,"month")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate("next_month")

# fin 2003

@numyear4:@modifierDate::DATE:
+AddEntityFeature(trigger,"numyear")
+AddEntityFeature(left.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

@annee=(année$NC,an$NC)

@annee:@modifierDate (de|(de l\')|d\'):t_integer?:DATE:
+AddEntityFeature(trigger,"year")
+AddEntityFeature(right.1,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

année::(dernière|passée|prochaine):DATE:
+AddEntityFeature(trigger,"year")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

an::(dernier|passé|prochain):DATE:
+AddEntityFeature(trigger,"year")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

mois::(dernier|passé|prochain):DATE:
+AddEntityFeature(trigger,"month")
+AddEntityFeature(right.1,"datemod")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()


# remove this one : dans les annees 90: not easy to normalize
#[@annee]::(t_integer|((<NUMBER>){1-3})):DATE:
#=>NormalizeDate()

#partial periods of years

semestre:(premier|second):((de (l\'année)?)? @numyear4)?:DATE:
trimestre:(premier|deuxième|troisième|quatrième):((de (l\'année)?)? @numyear4)?:DATE:
rentrée:[la]:(de (l\'année)?)? @numyear4:DATE:

@saison=(printemps$NC,été$NC,automne$NC,hiver$NC)
@saison:((début|milieu|fin) (de|(de l\')|d\'|du)?)?:(de (l\'année)?)? @numyear:DATE:


# centuries

siècle$NC:(t_ordinal_roman|t_cardinal_roman)::DATE:
+AddEntityFeature(left.1,"century")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()


siècle$NC:<NUMBER>::DATE:
+AddEntityFeature(left.1,"century:int")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

# 1984
# en 1979
# en Décembre

@in=(en,depuis)

@month:[(@in|au? mois de)?]::DATE:
+AddEntityFeature(trigger,"month")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeRelativeDate()

#------------------------------
# informal dates (to normalize, indicate the difference from the reference)
#aujourd\'::hui:DATE:
#=>NormalizeRelativeDate("diff_0")

#GC20130730: aujourd\'hui as dates are given category NC while they should remain adverbs.
# Should find a solution: specific EN type,  ? 

#aujourd\'hui:::DATE:
#=>NormalizeRelativeDate("date_document,diff_0d")

## ce matin, ce soir etc.
#@daytime:(ce|cet)::DATE:
#=>NormalizeRelativeDate("date_document,diff_0d")

#hier:::DATE:
#=>NormalizeRelativeDate("date_document,diff_-1d")

#demain:::DATE:
#=>NormalizeRelativeDate("date_document,diff_1d")

#après-demain:::DATE:
#=>NormalizeRelativeDate("date_document,diff_2d")

#------------------------------
# numeric forms (should be recognized by tokenizer in the future)

# 22-05-68 
# integer - integer - integer

@numday::- t_integer - t_integer:DATE:
+AddEntityFeature(trigger,"numday")
+AddEntityFeature(right.2,"month")
+AddEntityFeature(right.4,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

@numyear4::- t_integer - t_integer:DATE:
+AddEntityFeature(trigger,"numyear")
+AddEntityFeature(right.2,"month")
+AddEntityFeature(right.4,"numday")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

# 22/05/68 
# fraction / integer or integer / integer / integer

@numday::/ t_integer / t_integer:DATE:
+AddEntityFeature(trigger,"numday")
+AddEntityFeature(right.2,"month")
+AddEntityFeature(right.4,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()

# numbers between 1700 and 2050 are dates 
# unless followed by a unit

@numyear4::(de|d\')? $NC:NOT_DATE:

@numyear4:t_fraction /::DATE:
+AddEntityFeature(trigger,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()


@numyear4:::DATE:
+AddEntityFeature(trigger,"numyear")
=>NormalizeEntity()
=<ClearEntityFeatures()
=>NormalizeDate()
